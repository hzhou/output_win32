# --------------------------------------
#  register_wndclass, create_window, wndproc
# ---- Register Window Class ----
subcode: register_wndclass(name, proc)
    $local WNDCLASSEX wc
    wc.cbSize = sizeof(WNDCLASSEX);
    wc.hInstance = cur_instance
    wc.cbClsExtra = 0;
    BLOCK

    $(if:!wc_style)
        wc.style = 0

    $(if:!wc_icon)
        wc.hIcon = NULL
        wc.hIconSm = NULL

    $(if:!wc_cursor)
        wc.hCursor = LoadCursor(NULL, IDC_ARROW);

    $(if:!wc_background)
        wc.hbrBackground = (HBRUSH)(COLOR_BACKGROUND+1) # black
    $(if:!wc_menu)
        wc.lpszMenuName = NULL

    wc.lpszClassName = $(name)

    $(if:proc=DIALOG)
        wc.lpfnWndProc = DefDlgProc
        wc.cbWndExtra = DLGWINDOWEXTRA
    $(elif:proc:8=WndProc_)
        $(block:global_init)
            $wndproc $(proc)
        wc.lpfnWndProc = $(proc)
        wc.cbWndExtra = 0

    RegisterClassEx(&wc);

    # -------------------------
    subcode: cursor(p)
        $local WNDCLASSEX wc
        $(if:p:4=IDC_)
            wc.hCursor = LoadCursor(NULL, $(p))
            $(export:wc_cursor=1)

    subcode: style(p)
        $(if:wc_style!=1)
            wc.style=$(p)
        $(else)
            wc.style |= $(p)
        $(export:wc_style=1)

    subcode: icon(p)
        $(if:p:4=IDI_)
            $(set:r=MAKEINTRESOURCE($(p)))
            $(set:w=GetSystemMetrics(SM_$1ICON))
            wc.hIcon   = LoadImage(cur_instance, $(r), IMAGE_ICON, $(w:CX), $(w:CY), LR_DEFAULTCOLOR|LR_SHARED);
            wc.hIconSm = LoadImage(cur_instance, $(r), IMAGE_ICON, $(w:CXSM), $(w:CYSM), LR_DEFAULTCOLOR|LR_SHARED);
            $(export:wc_icon=1)

    subcode: menu(p)
        wc.lpszMenuName=(LPCTSTR)$(p)
        $(export:wc_menu=1)

    subcode: background(p)
        wc.hbrBackground=(HBRUSH)$(p)
        $(export:wc_background=1)

# ---- Create Window ----
subcode: create_window(hwnd, name)
    $(set:cw_x=CW_USEDEFAULT)
    $(set:cw_y=CW_USEDEFAULT)
    $(set:cw_w=CW_USEDEFAULT)
    $(set:cw_h=CW_USEDEFAULT)
    BLOCK
    $(if:!cw_style_ex)
        $(set:cw_style_ex=0)
    $(if:!cw_style)
        $(set:cw_style=WS_OVERLAPPEDWINDOW)
    $(hwnd)=CreateWindowEx($(cw_style_ex), $(name), $(name), $(cw_style), $(cw_x), $(cw_y), $(cw_w), $(cw_h), NULL, NULL, cur_instance, NULL)
    # ---------------------
    subcode: style_ex(p)
        $(if:!cw_style_ex)
            $(export:cw_style_ex=$(p))
        $(else)
            $(export:cw_style_ex=$(cw_style_ex)|$(p))

    subcode: style(p)
        $(if:!cw_style)
            $(export:cw_style=$(p))
        $(else)
            $(export:cw_style=$(cw_style)|$(p))

    subcode: size(x, y, w, h)
        $(export:cw_x=$(x))
        $(export:cw_y=$(y))
        $(export:cw_w=$(w))
        $(export:cw_h=$(h))

    subcode: dim(x, v)
        $(export:cw_$(x)=$(v))

# -- messages --
subcode: msg_destroy
    case WM_DESTROY:
        PostQuitMessage(0)
        return 1

